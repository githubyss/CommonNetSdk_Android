plugins {
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def isShellApp = project.getName() == rootProject.shellApp
def isLaunchApp = project.getName() == rootProject.launchApp

if (isShellApp) {
    println "isShellApp: ${isShellApp}"
}

if (isLaunchApp) {
    println "isLaunchApp: ${isLaunchApp}"
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}


apply from: "${rootDir}/config/buildConfigs.gradle"
apply from: "${rootDir}/config/optionConfigs.gradle"
apply from: "${rootDir}/config/featureExt.gradle"
apply from: "${rootDir}/config/versionExt.gradle"
apply from: "${rootDir}/config/signingConfigs.gradle"
apply from: "${rootDir}/config/dependencyExt.gradle"

android {
    resourcePrefix 'comnet_'

    defaultConfig {
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            if (isLaunchApp) {
                shrinkResources feature.isMinifyEnabled
            }
        }

        release {
            if (isLaunchApp) {
                shrinkResources true
            }
        }
    }

    flavorDimensions 'library'
    productFlavors {
        if (isLaunchApp) {
            app {
                dimension 'library'
                applicationId 'com.githubyss.mobile.common.net'
                versionCode 1
                versionName "1.0"
                manifestPlaceholders = [APP_NAME: "通用网络库"]
            }
        }
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/main/java'
                srcDirs 'src/main/kotlin'
            }
            res {
                srcDirs 'src/main/res'
                srcDirs 'src/main/res/common'
            }
            manifest {
                srcFile 'src/main/AndroidManifest.xml'
            }
            jniLibs {
                srcDirs 'libs'
            }
        }

        if (isLaunchApp) {
            app {
                java {
                    srcDirs 'src/app/java'
                    srcDirs 'src/app/kotlin'
                }
                res {
                    srcDirs 'src/app/res'
                    srcDirs 'src/app/res/common'

                    srcDirs 'src/app/res/homepage'
                }
                manifest {
                    srcFile 'src/app/AndroidManifest.xml'
                }
            }
        }

        test {
            java {
                srcDirs 'src/test/java'
                srcDirs 'src/test/kotlin'
            }
        }

        all {
            // set -> println "集合 ${set.name} 的文件是 ${set.java.srcDirs}"
        }
    }

    dataBinding {
        enabled feature.isDataBindingEnabled
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':DesignPatternLibrary_Android')
    implementation project(':CommonResSdk_Android')
    implementation project(':CommonBaseSdk_Android')
    implementation project(':CommonKitSdk_Android')

    /* Http */
    implementation dependenciesLib.apache_httpcore
    implementation dependenciesLib.apache_httpmime

    /* Volley */
    implementation dependenciesLib.mcxiaoke_volley

    /* Retrofit2 */
    implementation dependenciesLib.squareup_retrofit2
    implementation dependenciesLib.squareup_retrofit2_converter_moshi
}
